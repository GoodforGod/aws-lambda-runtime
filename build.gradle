plugins {
    id "jacoco"
    id "maven-publish"

    id "java-library"
    id "org.sonarqube" version "3.3"
    id "com.diffplug.spotless" version "5.14.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

group = groupId
version = artifactVersion

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

spotless {
    java {
        encoding("UTF-8")
        importOrder()
        removeUnusedImports()
        eclipse().configFile("${projectDir}/config/codestyle.xml")
    }
}

configurations {
    all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds' // check for updates every build
        }
    }
}

dependencies {
    annotationProcessor "io.goodforgod:graalvm-hint-processor:0.10.0-SNAPSHOT"

    compileOnly "io.goodforgod:graalvm-hint-annotations:0.10.0-SNAPSHOT"

    implementation "io.goodforgod:gson-configuration:1.0.0-SNAPSHOT"
    implementation "com.google.code.gson:gson:2.8.8"
    implementation "jakarta.inject:jakarta.inject-api:2.0.0"
    implementation "com.squareup.okhttp3:okhttp:3.14.9"
    implementation "org.eclipse.jetty.http2:http2-client:11.0.6"
    implementation 'org.eclipse.jetty.http2:http2-http-client-transport:11.0.6'
    implementation "org.eclipse.jetty:jetty-client:11.0.6"

    api "io.goodforgod:aws-lambda-java-events:1.0.0-SNAPSHOT"
    api "io.goodforgod:uri-builder:1.0.0-SNAPSHOT"
    api "org.jetbrains:annotations:22.0.0"
    api "com.amazonaws:aws-lambda-java-core:1.2.1"
    api "org.slf4j:slf4j-api:1.7.32"

    testRuntimeOnly "io.goodforgod:slf4j-simple-logger:0.10.0-SNAPSHOT"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.8.1"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    reports {
        html.enabled(false)
        junitXml.enabled(false)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Simple Lambda"
                url = "https://github.com/GoodforGod/$artifactId"
                description = "Fast, Lightweight and GraalVM optimized AWS Lambda Runtime."

                license {
                    name = "Apache License 2.0"
                    url = "https://github.com/GoodforGod/$artifactId/blob/master/LICENSE"
                    distribution = "repo"
                }

                developer {
                    id = "GoodforGod"
                    name = "Anton Kurako"
                    email = "goodforgod.dev@gmail.com"
                    url = "https://github.com/GoodforGod"
                }

                scm {
                    connection = "scm:git:git://github.com/GoodforGod/${artifactId}.git"
                    developerConnection = "scm:git:ssh://GoodforGod/${artifactId}.git"
                    url = "https://github.com/GoodforGod/$artifactId/tree/master"
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("OSS_USERNAME")
                password System.getenv("OSS_PASSWORD")
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: "signing"
    signing {
        sign publishing.publications.mavenJava
    }
}
